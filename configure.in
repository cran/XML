# An input file for autoconf to configure 
# the XML parsing facilities for both R and S.
# Currently this works for R.
#
# This was implemented by Friedrich Leisch
# with some modifications for the next version
# by Duncan Temple Lang.
#

AC_INIT(DESCRIPTION)

AC_ARG_WITH(libxml2,[--with-libxml2    indicate that the libxml version is 2.0 or higher],
               [ if test "${withval}" = yes; then
                    LIBXML2="-DLIBXML2=1";
                 fi])

AC_ARG_WITH(libxml,
  [  --with-libxml           use libxml library],
  [ if test "${withval}" = no; then
      USE_LIBXML=false;
    else
      USE_LIBXML=true;
    fi],
  USE_LIBXML=true)

# Default is false for expat since we can 
# do event driven parsing with libxml.
AC_ARG_WITH(expat,
  [  --with-expat            use expat library],
  [ if test "${withval}" = no; then
      USE_EXPAT=false;
    else
      USE_EXPAT=true;
    fi],
  USE_EXPAT=false)

if ${USE_LIBXML}; then
  if test -z ${LIBXML_INCDIR}; then
    AC_CHECK_HEADER(gnome-xml/parser.h, LIBXML_INCDIR="gnome-xml/")
  fi
  if test -z ${LIBXML_INCDIR}; then
    AC_CHECK_HEADER(libxml/parser.h, LIBXML_INCDIR="libxml/")
  fi
  if test -z ${LIBXML_INCDIR} ; then
   echo "Cannot find parser.h. Set the value of the environment variable"
   echo "    LIBXML_INCDIR"
   echo "to point to where it can be found."
   exit 1;
  else
   echo "Located parser file ${LIBXML_INCDIR}/parser.h"
  fi
#  AC_EGREP_HEADER(xmlParseFile, ${LIBXML_INCDIR}parser.h,
#	HAVE_LIBXML_HEADER=true,
#	AC_MSG_ERROR("header files for libxml seem to be incorrect"))


  AC_CHECK_LIB(z, gzopen)
  AC_CHECK_LIB(xml, xmlParseFile,, AC_MSG_ERROR("libxml not found"), "-L${LIBXML_LIBDIR-.}")

  if test -n "${LIBXML_LIBDIR}" ; then
     LIBS="${LIBS} -L${LIBXML_LIBDIR}"
     LD_PATH="${LIBXML_LIBDIR}"
  fi
  PKG_CPPFLAGS="${PKG_CPPFLAGS} -DLIBXML -I${LIBXML_INCDIR-.}"
fi

if ${USE_EXPAT}; then
  AC_CHECK_HEADER(xmltok/xmlparse.h, XMLPARSE_INCDIR="xmltok/")
  if test -z ${XMLPARSE_INCDIR}; then 
    AC_CHECK_HEADER(xmlparse/xmlparse.h, XMLPARSE_INCDIR="xmlparse/")
  fi
  AC_EGREP_HEADER(XML_Parse, ${XMLPARSE_INCDIR}xmlparse.h,
	HAVE_EXPAT_HEADER=true,
	AC_MSG_ERROR("header file xmlparse.h seems to be incorrect"))	
  AC_CHECK_LIB(xmltok, XmlInitEncoding,,AC_MSG_ERROR("libxmltok not found"))
  AC_CHECK_LIB(xmlparse, XML_Parse,,
		AC_MSG_ERROR("libxmlparse not found"), -lxmltok)
  PKG_CPPFLAGS="${PKG_CPPFLAGS} -DLIBEXPAT -I${XMLPARSE_INCDIR}"
  LD_PATH="${LD_PATH}:${LIBXML_LIBDIR}"
fi


if  ${USE_EXPAT} ; then
  SUPPORTS_EXPAT="TRUE"
else
  SUPPORTS_EXPAT="FALSE"
fi

echo "Expat: ${USE_EXPAT} ${SUPPORTS_EXPAT}"

if ${USE_LIBXML} ; then
  SUPPORTS_LIBXML="TRUE"
else
  SUPPORTS_LIBXML="FALSE"
fi

PKG_LIBS=${LIBS}

AC_SUBST(LIBXML2)

AC_SUBST(LIBXML_INCDIR)
AC_SUBST(XMLPARSE_INCDIR)
AC_SUBST(PKG_LIBS)
AC_SUBST(PKG_CPPFLAGS)

AC_SUBST(SUPPORTS_LIBXML)
AC_SUBST(SUPPORTS_EXPAT)

AC_SUBST(LD_PATH)

AC_OUTPUT(src/Makevars R/supports.R inst/scripts/RSXML.csh inst/scripts/RSXML.bsh)



